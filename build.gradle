buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.h2database:h2:1.4.191'
        classpath 'postgresql:postgresql:9.1-901-1.jdbc4'
    }
}

plugins {
    id "org.flywaydb.flyway" version "4.0.3"
}

flyway {
    url = "${dbHost}"
    schemas = ['apriori']
    table = "schema_history"
    user = 'apriori'
    password = "${dbPassword}"
    driver = "org.postgresql.Driver"
    locations = ['filesystem:resources/sql']
}

/**
 * Get Date
 * @return
 */
def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd HHmmssZ')
    return formattedDate
}

/**
 * Build docker image
 */
task build {
    doLast {
        def deployStream = new ByteArrayOutputStream()
        def date = getDate()

        exec {
            workingDir '.'
            commandLine 'lein', 'uberjar'
        }

        exec {
            standardOutput = deployStream
            workingDir '.'
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
        }

        exec{
            workingDir '.'
            commandLine 'docker', 'build',
                    '--build-arg', "VERSION=${version}",
                    '--build-arg', "BUILD_TIME=${date}",
                    '--build-arg', "GHASH=${deployStream.toString().trim()}",
                    '-t', "apriori:${version}", "."
        }
    }
}

/**
 * Run
 */
task run {
    doLast{
        exec {
            workingDir '.'
            commandLine 'docker-compose', 'up', '-d'
        }
    }
}

/**
 * Stop
 */
task stop {
    doLast {
        exec {
            workingDir '.'
            commandLine 'docker-compose', 'down'
        }
    }
}